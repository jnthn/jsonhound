#!/usr/bin/env perl6
use JSON::Fast;
use JsonHound::Reporter::CLI;
use JsonHound::RuleSet;

sub MAIN(
        $validations,  #= Path to module specifying the validation to apply
        *@json-files   #= The JSON file(s) to validate
        ) {
    my $*JSON-HOUND-RULESET = JsonHound::RuleSet.new;
    my $rule-file = $validations.IO;
    CompUnit::RepositoryRegistry.use-repository:
            CompUnit::RepositoryRegistry.repository-for-spec($rule-file.parent.absolute);
    require "$rule-file.basename()";

    my $reporter = JsonHound::Reporter::CLI.new;
    race for @json-files.race(:1batch) -> $file {
        with slurp($file) -> $json {
            with try from-json($json) -> $parsed {
                if $*JSON-HOUND-RULESET.validate($parsed) -> @violations {
                    $reporter.validation-error($file, @violations);
                }
                else {
                    $reporter.ok($file);
                }
            }
            else {
                $reporter.file-error($file, "invalid JSON: $!");
            }
        }
        else {
            $reporter.file-error($file, "file not found");
        }
    }
    exit($reporter.exit-code // 0);
}
